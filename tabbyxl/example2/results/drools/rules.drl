import ru.icc.td.tabbyxl.model.*

rule "#1" salience -1 lock-on-active
  when
    corner: CCell(cl == 1, rt == 1, blank);
    c: CCell(cr <= corner.cr, rt > corner.rb, !blank, !tagged);
  then
    c.setTag("stub");
    insert(c.newLabel());
    update(c);
end

rule "#2" salience -1 lock-on-active
  when
    corner: CCell(cl == 1, rt == 1, blank);
    c: CCell(rb <= corner.rb, cl > corner.cr, !blank, !tagged);
  then
    c.setTag("head");
    insert(c.newLabel());
    update(c);
end

rule "#3" salience -1 lock-on-active
  when
    corner: CCell(cl == 1, rt == 1, blank);
    c: CCell(cl > corner.cr, rt > corner.rb, !tagged);
  then
    c.setTag("body");
    insert(c.newEntry());
    update(c);
end

rule "#4"  salience -2 no-loop true
  when
    c1: CCell(tag == "stub");
    c2: CCell(tag == "stub", cl == c1.cl, cr == c1.cr);
  then
    c2.getLabel().group(c1.getLabel());
end

rule "#5" salience -2 no-loop true
  when
    c1: CCell(tag == "head");
    c2: CCell(tag == "head", rt == c1.rt, rb == c1.rb);
  then
     c2.getLabel().group(c1.getLabel());
end

rule "#6" salience -3 no-loop true
  when
    c1: CCell(tag == "stub");
    c2: CCell(tag == "body", rt >= c1.rt, rb <= c1.rb);
  then
    c2.getEntry().addLabel(c1.getLabel());
end

rule "#7" salience -3 no-loop true
  when
    c1: CCell(tag == "head");
    c2: CCell(tag == "body", cl >= c1.cl, cr <= c1.cr);
  then
    c2.getEntry().addLabel(c1.getLabel());
end